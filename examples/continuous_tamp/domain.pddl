(define (domain continuous-tamp)
  (:requirements :strips :equality)

  (:predicates
    (Robot ?r)
    (Block ?b)
    (Region ?s)
    (Pose ?b ?p)
    (Grasp ?b ?g)
    (Conf ?q)
    (Traj ?t)
    (Contain ?b ?p ?s) 
    (Kin ?b ?q ?p ?g)
    (Motion ?q1 ?t ?q2)
    (CFree ?b1 ?p1 ?b2 ?p2) 
    (Placeable ?b ?s)
    (PoseOnGround ?p) 
    (PoseOnPlatform ?p) 
    (StackPose ?p_above ?p_below) 
    (Assigned ?r ?b)

    (AtPose ?b ?p)
    (AtGrasp ?r ?b ?g)
    (AtConf ?r ?q) 
    (HandEmpty ?r)
    (CanMove ?r) 
    (CanManipulate ?r)
    (Clear ?b) 
    (On ?b_above ?b_below)
    (In ?b ?s)
  )

  (:functions
    (Cost)
    (total-cost)
    (Dist ?q1 ?q2)
  )

  (:action move
    :parameters (?r ?q1 ?t ?q2)
    :precondition (and
      (Robot ?r)
      (Motion ?q1 ?t ?q2) 
      (AtConf ?r ?q1)
      (CanMove ?r)
    )
    :effect (and
      (AtConf ?r ?q2)
      (CanManipulate ?r)
      (not (AtConf ?r ?q1))
      (not (CanMove ?r))
      (increase (total-cost) (Dist ?q1 ?q2))
    )
  )

  (:action pick
    :parameters (?r ?b ?p ?g ?q)
    :precondition (and
      (Robot ?r)
      (Assigned ?r ?b)
      (Kin ?b ?q ?p ?g)
      (AtConf ?r ?q)
      (AtPose ?b ?p)
      (HandEmpty ?r)
      (Clear ?b)
    )
    :effect (and
      (AtGrasp ?r ?b ?g)
      (CanMove ?r)
      (not (AtPose ?b ?p))
      (not (HandEmpty ?r))
      (not (CanManipulate ?r))
      (not (Clear ?b))
      (forall (?s)
        (when (Region ?s)
          (not (In ?b ?s))))
      (increase (total-cost) (Cost))
    )
  )

  (:action place-ground
    :parameters (?r ?b ?p ?g ?q)
    :precondition (and
      (Robot ?r)
      (Kin ?b ?q ?p ?g)
      (AtConf ?r ?q)
      (AtGrasp ?r ?b ?g)
      (PoseOnGround ?p)
    )
    :effect (and
      (AtPose ?b ?p)
      (HandEmpty ?r)
      (CanMove ?r)
      (Clear ?b)
      (not (AtGrasp ?r ?b ?g))
      (not (CanManipulate ?r))
      (forall (?s)
        (when (Contain ?b ?p ?s)
          (In ?b ?s)))
      (increase (total-cost) (Cost))
    )
  )

  (:action place-platform
    :parameters (?r ?b ?p ?g ?q)
    :precondition (and
      (Robot ?r)
      (Kin ?b ?q ?p ?g)
      (AtConf ?r ?q)
      (AtGrasp ?r ?b ?g)
      (PoseOnPlatform ?p)
    )
    :effect (and
      (AtPose ?b ?p)
      (HandEmpty ?r)
      (CanMove ?r)
      (Clear ?b)
      (not (AtGrasp ?r ?b ?g))
      (not (CanManipulate ?r))
      (forall (?s)
        (when (Contain ?b ?p ?s)
          (In ?b ?s)))
      (increase (total-cost) (Cost))
    )
  )

  (:action stack
    :parameters (?r ?b_above ?p_above ?g ?q ?b_below ?p_below)
    :precondition (and
      (Robot ?r)
      (Assigned ?r ?b_above)
      (Kin ?b_above ?q ?p_above ?g)
      (AtConf ?r ?q)
      (AtGrasp ?r ?b_above ?g)
      (AtPose ?b_below ?p_below)
      (StackPose ?p_above ?p_below)
      (Clear ?b_below)
    )
    :effect (and
      (AtPose ?b_above ?p_above)
      (HandEmpty ?r)
      (CanMove ?r)
      (Clear ?b_above)
      (On ?b_above ?b_below)
      (not (AtGrasp ?r ?b_above ?g))
      (not (CanManipulate ?r))
      (not (Clear ?b_below))
      (forall (?s)
        (when (In ?b_below ?s)
          (In ?b_above ?s)))
      (increase (total-cost) (Cost))
    )
  )
)
